import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.3.6"
project.ext.jomlVersion = "1.10.7"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		def osArch = System.getProperty("os.arch")
		if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
			project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
		} else if  (osArch.startsWith("ppc")) {
			project.ext.lwjglNatives += "-ppc64le"
		} else if  (osArch.startsWith("riscv")) {
			project.ext.lwjglNatives += "-riscv64"
		}
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
}

subprojects{
	group = "games.temporalstudio"

	repositories{
		mavenCentral()
	}
}

project(":engine"){
	apply plugin: "java-library"

	version = "0.0.0"

	dependencies{
		implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

		implementation "org.lwjgl:lwjgl"
		implementation "org.lwjgl:lwjgl-assimp"
		implementation "org.lwjgl:lwjgl-glfw"
		implementation "org.lwjgl:lwjgl-openal"
		implementation "org.lwjgl:lwjgl-opengl"
		implementation "org.lwjgl:lwjgl-stb"
		runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
		runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
		runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
		runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
		runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
		runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
		implementation "org.joml:joml:${jomlVersion}"
	}
}

project(":game"){
	apply plugin: "application"

	version = "0.0.0"

	dependencies{
		implementation project(':engine')
	}

	application{
		mainClass = "games.temporalstudio.timecapsule.Launcher"
	}
}